;===============================================
; Progressive.art
;
; A customizeable, text-based 
; progress bar generator & library
; for Arturo
;
; MIT License
; (c) 2024 Yanis Zafirópulos
;-----------------------------------------------
; @file src/progressBar.art
;===============================================

define :progressbar [
    ;---------------------------
    ; Style templates
    ;---------------------------

    defaultStyle:   to :block read ./"styles/default.art"
    plainStyle:     to :block read ./"styles/plain.art"
    fancyStyle:     to :block read ./"styles/fancy.art"

    ;---------------------------
    ; Constructor
    ;---------------------------

    init: method [maxValue :integer][
        ; initialize values
        \current: 0
        \maxValue: maxValue
        \run: 0

        ; initialize settings
        \label:         (lbl: <= attr 'label)? -> lbl -> "Downloading:"
        \processing:    ""
        \animated?:     true

        ; initialize style
        do \defaultStyle

        if attr 'plain -> do \plainStyle
        if attr 'fancy -> do \fancyStyle

        if bab: <= attr 'barBefore      -> \barBefore:      bab
        if baf: <= attr 'barAfter       -> \barAfter:       baf
        if onm: <= attr 'onMark         -> \onMark:         onm
        if onc: <= attr 'onColor        -> \onColor:        onc
        if ofm: <= attr 'offMark        -> \offMark:        ofm
        if ofc: <= attr 'offColor       -> \offColor:       ofc
        if lac: <= attr 'labelColor     -> \labelColor:     lac
        if dtc: <= attr 'detailColor    -> \detailColor:    dtc

        \animated?:     null? attr 'static
        \hasBar?:       null? attr 'barless
        \hasRatio?:     null? attr 'ratioless

        \hasDetails?:   null? attr 'simple
        \persistent?:   null? attr 'minimize

        \animationSize: \animated? ? -> size first \animations
                                     -> 0

        ; let's start
        \startTime: to :integer now
        \active?: true

        ; hide the cursor
        cursor false
    ]

    ;---------------------------
    ; Helpers
    ;---------------------------

    width: function [][
       terminal\width
    ]

    space: function [spacelen][
        repeat " " spacelen
    ]
    
    neat: function [strblk][
        join.with:" " @ strblk
    ]

    colored: function [col, str][
        (col = #white)? -> str
                        -> color col str
    ]

    friendlyTime: function [num][
        if num = 0      -> return "< 1s"
        if num < 60     -> return ~"~ |num|s"
        if num = 60     -> return "< 1min"
        if num < 60*60  -> return ~"~ |num / 60|min |num % 60|s"
        if num = 60*60  -> return "< 1h"
        
        [hours,rest]    : divmod num 60*60
        [mins,secs]     : divmod rest 60 

        return ~"~ |hours|h |mins|min |secs|s"
    ]

    ;---------------------------
    ; Private methods
    ;---------------------------

    goBack: method [][
        prints repeat "\b" \width * enumerate @[\hasDetails? \hasBar?] => [&]
    ]

    cleanUp: method [][
        \goBack
        prints \space \width * enumerate @[\hasDetails? \hasBar?] => [&]
        \goBack
    ]

    timeLeft: method [][
        timeUpToNow: (to :integer now) - \startTime
        remaining: div (\maxValue - \current) * timeUpToNow \current
        return \neat [\friendlyTime remaining, "left"]
    ]

    timeTaken: method [][
        timeUpToNow: (to :integer now) - \startTime
        return \neat ["Completed in", replace \friendlyTime timeUpToNow "~ " ""]
    ]

    percentage: method [][
        pad.left (to :string .format:".1f" round.to: 1 (\current * 100) // \maxValue) ++ "%" 6
    ]

    detailLine: method [][
        if not? \hasDetails? -> return ""

        currentlyProcessing: ""
        if not? empty? \processing ->
            currentlyProcessing: (\hasRatio? ? -> "➔ " -> "") ++ \processing

        dtlText: (\hasRatio?)? -> \neat [~{|\current|/|\maxValue|} currentlyProcessing]
                               -> currentlyProcessing

        (not? \hasBar?)? 
            -> (\space (\animated? ? -> inc \animationSize -> 0) + 1) ++ \label ++ " " ++ color \detailColor dtlText ++ (pad.left \timeLeft \width - sum @[2 + size \label, size dtlText, (\animated? ? -> inc \animationSize -> 0), 8]) ++ " " ++ color #green \percentage
            -> (\space (\animated? ? -> inc \animationSize -> 0) + 2 + (size \label) + size \barBefore) ++ color \detailColor dtlText ++ (pad.left \timeLeft \width - sum @[2 + size \label, size dtlText, (\animated? ? -> inc \animationSize -> 0), 8, size \barBefore, size \barAfter]) ++ \space 8 + size \barAfter
    ]

    showFinalDetails: method [][
        prints (not? \hasDetails?)? -> "" [
            " " ++ (color \detailColor pad.left \timeTaken \width - sum @[(size \label) - 3, size \barAfter]) ++ \space 8 + size \barAfter
        ]
    ]

    barLine: method [][
        if not? \hasBar? -> return ""

        pcent: \percentage
        spaceAvailable: \width - sum @[inc size \label, size pcent, 3, \animated? ? -> inc \animationSize -> 0, size \barBefore, size \barAfter]

        ons: (\current * spaceAvailable) / \maxValue
        offs: spaceAvailable - ons

        bar: (color \labelColor \barBefore) ++ (\colored \onColor repeat \onMark ons) ++ (\colored \offColor repeat \offMark offs) ++ (color \labelColor \barAfter)

        pr: \animations\[\run % size \animations]
        return join.with:" " @[(\animated? ? -> " " ++ pr -> "") ++ " " ++ color.bold \labelColor \label bar color #green pcent]
    ]

    showFinalLine: method [][
        prints " " ++ (color.bold #white \label) ++ " " ++ "Completed in " ++ (replace \friendlyTime (to :integer now) - \startTime "~ " "") ++ " " ++ color #green "✔"
    ]

    ;---------------------------
    ; Public methods
    ;---------------------------

    increase: method [q :integer :floating][
        \current: \current + q
        if \animated? -> \run: \run + 1

        \goBack
        prints \detailLine
        prints \barLine

        if \current >= \maxValue [
            \active?: false
            \stop
        ]
    ]

    stop: method [][
        \cleanUp
        (and? \persistent? \hasBar?)?[
            \showFinalDetails
            prints \barLine
        ][
            \showFinalLine
        ]
        \active?: false
        print ""
        cursor true
    ]
]