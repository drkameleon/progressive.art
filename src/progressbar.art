;===============================================
; Progressive.art
;
; A customizeable, text-based 
; progress bar generator & library
; for Arturo
;
; MIT License
; (c) 2024 Yanis Zafirópulos
;-----------------------------------------------
; @file src/progressBar.art
;===============================================

define :progressbar [
    ;---------------------------
    ; Style templates
    ;---------------------------

    defaultStyle:   to :block read ./"styles/default.art"
    plainStyle:     to :block read ./"styles/plain.art"
    fancyStyle:     to :block read ./"styles/fancy.art"

    ;---------------------------
    ; Constructor
    ;---------------------------

    init: method [maxValue :integer][
        ; initialize values
        \current: 0
        \maxValue: maxValue
        \run: 0
        
        do \defaultStyle

        \animations: to [:string] split "┤┘┴└├┌┬┐"
        \animated?: null? attr 'noSpinner
        \hasDetails?: null? attr 'noDetails
        \hasBar?: null? attr 'noBar
        \hasRatio?: null? attr 'noRatio

        \label: (lbl: <= attr 'label)? -> lbl -> "Downloading:"
        \processing: ""

        \barBefore: (bab: <= attr 'barBefore)? -> bab -> ""
        \barAfter: (baf: <= attr 'barAfter)? -> baf -> ""
        \onMark: (onm: <= attr 'onMark)? -> onm -> "▓"
        \onColor: (onc: <= attr 'onColor)? -> onc -> #white
        \offMark: (offm: <= attr 'offMark)? -> offm -> "░"
        \offColor: (offc: <= attr 'offColor)? -> offc -> #white
        \labelColor: (lac: <= attr 'labelColor)? -> lac -> #white
        \persistent?: null? attr 'minimize
        \width: (wdt: <= attr 'width)? -> wdt -> 0

        if not? null? attr 'plain -> do \plainStyle
        if not? null? attr 'fancy -> do \fancyStyle

        \animationSize: \animated? ? -> size first \animations
                                     -> 0

        ; let's start
        \startTime: to :integer now
        \active?: true

        ; hide the cursor
        cursor false
    ]

    ;---------------------------
    ; Helpers
    ;---------------------------

    colored: function [col, str][
        (col = #white)? -> str
                        -> color col str
    ]

    ;---------------------------
    ; Private methods
    ;---------------------------

    lineWidth: method [][
        (\width = 0)? -> terminal\width
                      -> \width
    ]

    goBack: method [][
        prints repeat "\b" \lineWidth * enumerate @[\hasDetails? \hasBar?] => [&]
    ]

    cleanUp: method [][
        prints repeat " " \lineWidth * enumerate @[\hasDetails? \hasBar?] => [&]
    ]

    friendlyTime: method [num][
        if num = 0 -> return "< 1s"
        if num < 60 -> return ~"~ |num|s"
        if num = 60 -> return "< 1min"
        if num < 60*60 -> return ~"~ |num / 60|min |num % 60|s"
        if num = 60*60 -> return "< 1h"
        
        hours: num / 60*60
        rest: num % 60*60
        mins: rest / 60
        secs: rest % 60
        return ~"~ |hours|h |mins|min |secs|s"
    ]

    showDetailLine: method [][
        prints (not? \hasDetails?)? -> "" [
            currentlyProcessed: ""
            if not? empty? \processing ->
                currentlyProcessed: (\hasRatio? ? -> "➔ " -> "") ++ \processing

            dtlText: (\hasRatio?)? -> ~{|\current|/|\maxValue| |currentlyProcessed|}
                                   -> ~{|currentlyProcessed|}

            timeUpToNow: (to :integer now) - \startTime
            remaining: ((\maxValue - \current) * timeUpToNow) / \current
            remaining: (\friendlyTime remaining) ++ " left"
            (not? \hasBar?)? 
                [
                    pcent: pad.left (to :string .format:".1f" round.to: 1 (\current * 100) // \maxValue) ++ "%" 6
                    (repeat " " (\animated? ? -> inc \animationSize -> 0) + 1) ++ \label ++ " " ++ color #gray dtlText ++ (pad.left remaining \lineWidth - sum @[2 + size \label, size dtlText, (\animated? ? -> inc \animationSize -> 0), 8]) ++ " " ++ color #green pcent
                ]
                -> (repeat " " (\animated? ? -> inc \animationSize -> 0) + 2 + (size \label) + size \barBefore) ++ color #gray dtlText ++ (pad.left remaining \lineWidth - sum @[2 + size \label, size dtlText, (\animated? ? -> inc \animationSize -> 0), 8, size \barBefore, size \barAfter]) ++ repeat " " 8 + size \barAfter
        ]
    ]

    showFinalDetails: method [][
        prints (not? \hasDetails?)? -> "" [
            " " ++ (color #gray pad.left "Completed in " ++ replace \friendlyTime (to :integer now) - \startTime "~ " "" \lineWidth - sum @[(size \label) - 3, size \barAfter]) ++ repeat " " 8 + size \barAfter
        ]
    ]

    showBarLine: method [][
        (not? \hasBar?)? -> prints "" [
            pcent: pad.left (to :string .format:".1f" round.to: 1 (\current * 100) // \maxValue) ++ "%" 6
            spaceAvailable: \lineWidth - sum @[inc size \label, size pcent, 3, \animated? ? -> inc \animationSize -> 0, size \barBefore, size \barAfter]

            ons: (\current * spaceAvailable) / \maxValue
            offs: spaceAvailable - ons

            bar: (color \labelColor \barBefore) ++ (\colored \onColor repeat \onMark ons) ++ (\colored \offColor repeat \offMark offs) ++ (color \labelColor \barAfter)

            pr: \animations\[\run % size \animations]
            prints [(\animated? ? -> " " ++ pr -> "") ++ " " ++ color.bold \labelColor \label bar color #green pcent]
        ]
    ]

    showFinalLine: method [][
        prints " " ++ (color.bold #white \label) ++ " " ++ "Completed in " ++ (replace \friendlyTime (to :integer now) - \startTime "~ " "") ++ " " ++ color #green "✔"
    ]

    ;---------------------------
    ; Public methods
    ;---------------------------

    increase: method [q :integer :floating][
        \current: \current + q
        if \animated? -> \run: \run + 1

        \goBack
        \showDetailLine
        \showBarLine

        if \current >= \maxValue [
            \active?: false
            \stop
        ]
    ]

    stop: method [][
        \goBack
        \cleanUp
        \goBack
        (and? \persistent? \hasBar?)?[
            \showFinalDetails
            \showBarLine
        ][
            \showFinalLine
        ]
        \active?: false
        print ""
        cursor true
    ]
]